apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'net.researchgate.release'
apply plugin: 'co.riiid.gradle'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'


def travisTag = System.getenv("TRAVIS_TAG") ?: ""


if (!travisTag.empty) {
    // Only activate when we're building a tag (release)
    if (System.getenv("GITHUB_API_TOKEN") == null) {
        throw new GroovyRuntimeException("GITHUB_API_TOKEN not set")
    }
    println("Trying to build release")
    check.finalizedBy githubRelease
}

github {
    owner = 'SourceForgery'
    repo = 'tachikoma'
    token = System.getenv("GITHUB_API_TOKEN") ?: "xx"
    tagName = travisTag
    targetCommitish = 'master'
    name = "v${project.version}"
    assets = ["${project.buildDir}/kubernetes/deployment-webserver.yaml"]
}


def dockerPushRelease = false
def currentBranch = System.getenv("TRAVIS_BRANCH") ?: grgit.branch.current.name
if (currentBranch == "master" && Boolean.valueOf(System.getenv("DOCKER_PUSH"))) {
    dockerPushRelease = true
}
if (!travisTag.empty) {
    dockerPushRelease = true
}

ext {
    dockerPush = dockerPushRelease
}

bintray {
    publish = true
    publications = []
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    pkg {
        repo = 'Tachikoma'
        name = 'tachikoma'
    }
}


subprojects {
    if (!travisTag.empty) {
        check.finalizedBy bintrayUpload
    }
}
