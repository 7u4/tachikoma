plugins {
    id "com.jtitor.rust" version "0.1.4"
    id "base"
}

configurations {
    api
}

dependencies {
    api project(":tachikoma-backend-api-proto")
    api "com.google.protobuf:protobuf-java:$protoc_version"
}

artifacts {
    archives file: file("$buildDir/${archivesBaseName}-${project.version}.zip"), name: archivesBaseName, type: 'zip'
}

task apiCopy(type: Copy) {
    configurations.api.asFileTree.each {
        from(zipTree(it))
        include '**/*.proto'
    }
    into file("${buildDir}/grpc-api/")
}

task zipBinaries(type: Zip) {
    if (file('target/debug').exists() && file('target/release').exists()) {
        throw new Exception("Pick target/debug or target/release")
    }
    from 'target/debug/'
    from 'target/release/'
    include 'tachikoma_*'
    exclude '*.d'
    archiveName "${archivesBaseName}-${version}.zip"
    destinationDir(buildDir)
}
assemble.dependsOn apiCopy
assemble.dependsOn rustBuildTop
assemble.dependsOn zipBinaries
zipBinaries.shouldRunAfter rustBuildTop
