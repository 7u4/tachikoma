apply from: "${project.rootDir}/docker.gradle"

configurations {
    binaries
}

dependencies {
    binaries project(path: ":tachikoma-webserver", configuration: 'archives')
}

task webserverDocker([type: Docker, dependsOn: configurations.binaries]) {
    dependsOn downloadTini

    applicationName = 'tachikoma-webserver'

    baseImage 'ubuntu:xenial'

    maintainer 'tachikoma@sourceforgery.com'

    setEnvironment('DEBIAN_FRONTEND', 'noninteractive')

    runCommand('apt-get update && apt-get -y --no-install-recommends install less nvi openjdk-8-jdk-headless && apt-get clean && rm -rf /var/lib/apt/lists/*')

    exposePort(8443)
    exposePort(8070)

    addFile(project.extensions.extraProperties.tiniBinary)

    runCommand('chmod a+rx /tini')

    addFile {
        configurations.binaries.filter {
            it.path.endsWith(".tar")
        }
        .each {
            from(tarTree(it))
            includeEmptyDirs = false
            eachFile { FileCopyDetails fileCopyDetails ->
                fileCopyDetails.path = fileCopyDetails.path.replaceAll("^[^/]*", "/opt/tachikoma-webserver")
            }
        }
    }

    runCommand("chmod a+x /opt/tachikoma-webserver/bin/tachikoma-webserver")

    runCommand("useradd webserver && mkdir -p /var/log/tachikoma && chown webserver:root /var/log/tachikoma")

    entryPoint(['/tini', '--'])

    defaultCommand(["/opt/tachikoma-webserver/bin/tachikoma-webserver"])
    push = rootProject.extensions.extraProperties.dockerPush
}

assemble.dependsOn webserverDocker
