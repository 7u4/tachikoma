buildscript {
    dependencies {
        classpath "co.riiid:gradle-github-plugin:0.4.2"
        classpath "com.google.gradle:osdetector-gradle-plugin:1.4.0"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.8"
        classpath("com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4") {
            exclude group: 'nekohtml', module: 'xercesMinimal'
        }
        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.1.1"
        classpath "io.ebean:ebean-gradle-plugin:12.1.1"
        classpath "net.researchgate:gradle-release:2.8.0"
        classpath "org.ajoberstar.grgit:grgit-gradle:4.0.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.2.0"
        classpath "se.transmode.gradle:gradle-docker:1.2"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.0"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://dl.bintray.com/jetbrains/spek/" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap/" }
    }
}

apply from: 'setuprelease.gradle'

apply plugin: 'com.github.ben-manes.versions'

task replaceVersion(type: Copy) {
    from('kubernetes') {
        include '**/*.yaml'
        expand([version: project.version])
    }
    into "${project.buildDir}/kubernetes/"
    includeEmptyDirs = false
}

assemble.dependsOn replaceVersion

subprojects {
    apply plugin: 'idea'

    configurations {
        compile.exclude(group: 'org.glassfish.hk2.external', module: 'javax.inject')
    }

    idea {
        module {
            outputDir = file('build/idea-out')
            testOutputDir = file('build/idea-testout')
        }
    }

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    sourceCompatibility = ext.java_version
    targetCompatibility = ext.java_version
}

group 'com.sourceforgery.tachikoma'
