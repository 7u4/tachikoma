apply from: "${project.rootDir}/docker.gradle"

configurations {
    binaries
}

dependencies {
    binaries project(path: ":tachikoma-postfix-utils", configuration: 'archives')
}

task postfixDocker([type: Docker, dependsOn: configurations.binaries]) {
    applicationName = 'tachikoma-postfix'

    baseImage 'ubuntu:19.10'

    maintainer 'tachikoma@sourceforgery.com'

    workingDir("/opt")

    setEnvironment('DEBIAN_FRONTEND', 'noninteractive')
    setEnvironment('PYTHONIOENCODING', 'utf-8')

    runCommand('''apt-get update && \
                  apt-get -y dist-upgrade && \
                  apt-get -y --no-install-recommends install rsyslog rsyslog-gnutls python-pip python-pkg-resources less nvi postfix sasl2-bin opendkim opendkim-tools openjdk-11-jdk-headless netcat-openbsd && \
                  apt-get clean && \
                  rm -rf /var/cache/apt/ /var/lib/apt/lists/*
              ''')
    runCommand('pip install --no-cache-dir "honcho==1.0.1"')

    exposePort(25)

    addFile(file('src/assets/'))

    defaultCommand(['/usr/local/bin/honcho', 'start', '-f', 'Procfile'])

    runCommand("mkfifo /opt/maillog_pipe && chown syslog:postfix /opt/maillog_pipe")
    runCommand("mkdir -p /var/spool/postfix/tachikoma/")

    runCommand("sed -r '/(KLogPermitNonKernelFacility|imklog)/d' -i /etc/rsyslog.conf")
    runCommand("sed -r 's/\\|(.*)xconsole\$/\\1console/' -i /etc/rsyslog.d/50-default.conf")

    runCommand('adduser syslog postfix')
    runCommand('adduser postfix opendkim')
    runCommand('useradd tachikoma -d /opt')

    runCommand("chown postfix:tachikoma /var/spool/postfix/tachikoma/ && chmod 0770 /var/spool/postfix/tachikoma/")

    addFile {
        configurations.binaries.filter {
            it.path.endsWith(".tar")
        }
        .each {
            from(tarTree(it))
            includeEmptyDirs = false
            eachFile { FileCopyDetails fileCopyDetails ->
                fileCopyDetails.path = fileCopyDetails.path.replaceAll("^[^/]*", "/opt/tachikoma-postfix-utils")
            }
        }
    }

    runCommand("chmod a+x /opt/tachikoma-postfix-utils/bin/tachikoma-postfix-utils")

    push = rootProject.extensions.extraProperties.dockerPush
}

dockerTask.dependsOn postfixDocker
