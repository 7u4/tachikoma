apply plugin: 'base'
apply plugin: 'docker'

configurations {
    binaries
}

dependencies {
    binaries project(path: ":tachikoma-postfix-utils", configuration: 'archives')
}

task postfixDocker([type: Docker, dependsOn: configurations.binaries]) {
    applicationName = 'tachikoma-postfix'

    baseImage 'ubuntu:xenial'

    maintainer 'tachikoma@sourceforgery.com'

    workingDir("/opt")

    setEnvironment('DEBIAN_FRONTEND', 'noninteractive')

    runCommand('apt-get update && apt-get -y --no-install-recommends install inetutils-syslogd less nvi supervisor postfix sasl2-bin opendkim opendkim-tools openjdk-8-jdk-headless && apt-get clean && rm -rf /var/lib/apt/lists/*')

    addFile(file('src/assets/'))

    runCommand('./opendkim.sh')

    defaultCommand(['/usr/bin/supervisord', '-c', '/etc/supervisor/supervisord.conf'])

    runCommand("mkfifo /opt/maillog_pipe && chown postfix:postfix /opt/maillog_pipe")

    runCommand('mkdir -p /var/log/tachikoma && chown postfix:postfix /var/log/tachikoma')

    addFile {
        configurations.binaries.filter {
            it.path.endsWith(".tar")
        }
        .each {
            from(tarTree(it))
            includeEmptyDirs = false
            eachFile { FileCopyDetails fileCopyDetails ->
                fileCopyDetails.path = fileCopyDetails.path.replaceAll("^[^/]*", "/opt/tachikoma-postfix-utils")
            }
        }
    }

    runCommand("chmod a+x /opt/tachikoma-postfix-utils/bin/tachikoma-postfix-utils")
}

assemble.dependsOn postfixDocker
