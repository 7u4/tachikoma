buildscript {
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

configurations {
    binaries
}

dependencies {
    binaries project(path: ":tachikoma-postfix-utils", configuration: 'archives')
}


apply plugin: 'base'
apply plugin: 'docker'

def workDir = 'opt'

task postfixDocker(type: Docker) {
    applicationName = 'tachikoma-postfix'

    baseImage 'ubuntu:xenial'

    maintainer 'tachikoma@sourceforgery.com'

    workingDir("/$workDir")

    setEnvironment('DEBIAN_FRONTEND', 'noninteractive')

    runCommand('apt-get update && apt-get -y --no-install-recommends install less nvi supervisor postfix opendkim opendkim-tools openjdk-8-jdk-headless && apt-get clean && rm -rf /var/lib/apt/lists/*')

    addFile(file('src/assets/'))

    runCommand('./opendkim.sh')

    defaultCommand(['/usr/bin/supervisord', '-c', '/etc/supervisor/supervisord.conf'])

    addFile {
        configurations.binaries.filter {
            it.path.endsWith(".tar")
        }
        .each {
            from(tarTree(it))
            includeEmptyDirs = false
            eachFile { FileCopyDetails fileCopyDetails ->
                fileCopyDetails.path = fileCopyDetails.path.replaceAll("^[^/]*", "$workDir/tachikoma-postfix-utils")
            }
        }
    }
}

assemble.dependsOn postfixDocker
